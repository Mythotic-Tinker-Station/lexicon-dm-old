ACTOR Lexicon_DW12_Arachnotron : Arachnotron
{
	States
	{
	Missile:
		BSPI A 20 Bright A_FaceTarget
		TNT1 A 0 A_FaceTarget
		BSPI G 4 Bright A_CustomMissile("Lexicon_DW12_ArachnotronPlasma")
		BSPI H 4 Bright
		BSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	}
}

ACTOR Lexicon_DW12_ArachnotronPlasma : ArachnotronPlasma { SeeSound "DW12/DSPLASMA" }

ACTOR Lexicon_DW12_PlasmaRifle : DoomWeapon
{
	Weapon.SelectionOrder 100
	Weapon.AmmoUse 1
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Weapon.SlotNumber 6
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTPLASMA"
	Inventory.PickupSound "DW12/DSWPNUP"
	Tag "$TAG_PLASMARIFLE"
	States
	{
	Ready:
		PLSG A 1 A_WeaponReady
		Loop
	Deselect:
		PLSG A 1 A_Lower
		Loop
	Select:
		PLSG A 1 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_GunFlash("Flash",GFF_NOEXTCHANGE)
		PLSG A 3 A_FireCustomMissile("Lexicon_DW12_PlasmaBall")
		PLSG B 20 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Light1
		PLSF A 4 Bright A_Jump(128,1)
		Goto LightDone
		PLSF B 4 Bright
		Goto LightDone
	Spawn:
		PLAS A -1
		Stop
	}
}

ACTOR Lexicon_DW12_PlasmaBall : PlasmaBall { SeeSound "DW12/DSPLASMA" DeathSound "DW12/DSFIRXPL" }

ACTOR Lexicon_DW12_BFG9000 : DoomWeapon
{
	Height 20
	Weapon.SelectionOrder 2800
	Weapon.AmmoUse 40
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Weapon.SlotNumber 7
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOLMS
	+WEAPON.CHEATNOTWEAPON
	Inventory.PickupMessage "$GOTBFG9000"
	Inventory.PickupSound "DW12/DSWPNUP"
	Tag "$TAG_BFG9000"
	States
	{
	Ready:
		BFGG A 1 A_WeaponReady
		Loop
	Deselect:
		BFGG A 1 A_Lower
		Loop
	Select:
		BFGG A 1 A_Raise
		Loop
	Fire:
		BFGG A 20 A_PlaySound("DW12/DSBFG",CHAN_WEAPON)
		BFGG B 10 A_GunFlash
		BFGG B 10 A_FireBFG
		BFGG B 20 A_ReFire
		Goto Ready
	Flash:
		BFGF A 11 Bright A_Light1
		BFGF B 6 Bright A_Light2
		Goto LightDone
	Spawn:
		BFUG A -1
		Stop
	OldFire:
		BFGG A 10 A_PlaySound("DW12/DSBFG",CHAN_WEAPON)
		BFGG BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_FireOldBFG
		BFGG B 0 A_Light0
		BFGG B 20 A_ReFire
		Goto Ready
	}
}